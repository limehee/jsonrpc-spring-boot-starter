plugins {
    id 'base'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
        withJavadocJar()
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        testImplementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = project.name
                    description = 'JSON-RPC 2.0 library and Spring Boot starter'
                    url = 'https://github.com/limehee/jsonrpc-spring-boot-starter'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'limehee'
                            name = 'limehee'
                        }
                    }
                    scm {
                        url = 'https://github.com/limehee/jsonrpc-spring-boot-starter'
                        connection = 'scm:git:git://github.com/limehee/jsonrpc-spring-boot-starter.git'
                        developerConnection = 'scm:git:ssh://github.com/limehee/jsonrpc-spring-boot-starter.git'
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'ossrh'
                def releasesRepoUrl = uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
                def snapshotsRepoUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
                url = version.toString().endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME')
                    password = findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD')
                }
            }
        }
    }

    signing {
        def signingKey = findProperty('signingKey') ?: System.getenv('SIGNING_KEY')
        def signingPassword = findProperty('signingPassword') ?: System.getenv('SIGNING_PASSWORD')
        if (signingKey && signingPassword) {
            useInMemoryPgpKeys(signingKey, signingPassword)
        }
        required = !version.toString().endsWith('SNAPSHOT')
        sign publishing.publications
    }
}
